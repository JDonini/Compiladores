<program> ::= <sequenciaDeclaracao>

<declaracao> ::= <variaveldeclaracao>
       | id '(' <listaParametro> ')' '{' <bloco> '}'
       | <tipo> id '(' <listaParametro> ')' '{' <bloco> '}'

<variaveldeclaracao> ::= <tipo> <variavelSpecificacaoSeq> ';'

<variavelSpecificacao> ::= id
           | id '=' <valor>
           | id '[' num ']'
           | id '[' num ']' '=' '{' <sequenciavalor> '}'

<tipo> ::= "int"
        |  "stringing"
        |  "bool"

<parametro> ::= <tipo> id
              | <tipo> id '[' ']'

<bloco> ::= <variaveldeclaracaoList> <listacomando>

<comando> ::= <comandoIf>
          |   <comandoWhile>
          |   <comandoFor>
          |   <comandoBreak>
          |   <comandoReturn>
          |   <comandoRead>
          |   <comandoWrite>
          |   <expressaoAtrib> ';'
          |   <chamadaDeFuncao> ';'

<comandoIf> ::= "if" '(' <expressao> ')' '{' <bloco> '}'
              | "if" '(' <expressao> ')' '{' <bloco> '}' "else" '{' <bloco> '}'

<comandoWhile> ::= "while" '(' <expressao> ')' '{' <bloco> '}'

<comandoFor> ::= "for" '(' <expressaoAtrib> ';' <expressao> ';' <expressaoAtrib> ')' '{' <bloco> '}'

<comandoBreak> ::= "break" ';'

<comandoRead> ::= "read" <variavel> ';'

<comandoWrite> ::= "write" <listaexpressaoeracao> ';'

<comandoReturn> ::= "return" ';'
                  | "return" <expressao> ';'

<chamadaDeFuncao> ::= id '(' <listaexpressaoeracao> ')'

<op> ::= '-' | '!' | '*' | '/' | '%' | '+' | '<' | '<=' | '>' | '>=' | '==' | '!=' | '&&' | '||'

<opAtrib> ::= '=' | '+=' | '-='|'*='|'/='|'%='

<expressaoAtrib> ::= <variavel> <opAtrib>  <expressao>

<variavel> ::= id
             | id '[' <expressao> ']'

<expressao> ::= <expressao> <op> <expressao>
             | '!' <expressao>
             | '-' <expressao>
             | <expressao> '?' <expressao> ':' <expressao>
             | <chamadaDeFuncao>
             | <variavel>
             | <valor>
             | '(' <expressao> ')'

<valor> ::= num
          | string
          | logica

<listaParametro> ::= <sequenciaParametro>
                    | ε

<sequenciaParametro> ::= <parametro> ',' <sequenciaParametro>
                      |  <parametro>

<variaveldeclaracaoList> ::= <variaveldeclaracao> <variaveldeclaracaoList>
                          | ε

<variavelSpecificacaoSeq> ::= <variavelSpecificacao> ',' <variavelSpecificacaoSeq>
                          |   <variavelSpecificacao>

<sequenciaDeclaracao> ::= <declaracao> <sequenciaDeclaracao>
                      |   <declaracao>

<listacomando> ::= <comando> <listacomando>
                | ε

<sequenciavalor> ::= <valor> <sequenciavalor>
                  |  <valor>

<listaexpressaoeracao> ::= <sequenciaexpressaoeracao>
                          | ε

<sequenciaexpressaoeracao> ::= <expressao> ',' <sequenciaexpressaoeracao>
                            |  <expressao>
